// develop/src/main/java/com/twitter/teruteru128/study/ui

plugins {
    //id 'com.twitter.teruteru128.java.study.java-conventions'
    // https://docs.gradle.org/current/userguide/java_plugin.html
    id 'java'
    // https://docs.gradle.org/current/userguide/eclipse_plugin.html
    id 'eclipse'
    // Apply the application plugin to add support for building a CLI application in Java.
    // https://docs.gradle.org/current/userguide/application_plugin.html
    id 'application'
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    //id 'java-library'
    // https://github.com/java9-modularity/gradle-modules-plugin
    id 'org.javamodularity.moduleplugin' version '1.8.10' apply true
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainModule = 'com.twitter.teruteru.ui'
    mainClass = 'com.twitter.teruteru128.ui.Main'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
  
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// thanks for https://github.com/eclipse/eclipse.jdt.ls/issues/957
eclipse {
    /* 
    doFirst {
        File prefs = file("${project.projectDir}/.settings/org.eclipse.buildship.core.prefs")
        if(!prefs.exists()) {
            prefs.append('''
                connection.project.dir=
                eclipse.preferences.version=1
                '''.stripIndent())
        }
    } */
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.find { it.path == 'src/main/java' }.output = 'bin/main'

                entries.findAll { it.path.startsWith('/') && !it.path.endsWith('.jar') }.each {
                    it.entryAttributes['module'] = 'true'
                }

                def testSrc = entries.find { it.path == 'src/test/java' }
                testSrc.output = 'bin/test'
                testSrc.entryAttributes['test'] = 'true'
                testSrc.entryAttributes['optional'] = 'true'
                testSrc.entryAttributes['ignore_optional_problems'] = 'true'

                entries.forEach { entry ->
                    def entryIn = { it.find { file(entry.path).equals(it) } }
                    if (entry.kind == 'lib') {
                        entry.entryAttributes['test'] =
                            entryIn(configurations.testRuntimeClasspath) &&
                            !entryIn(configurations.runtimeClasspath)
                    }
                }
            }
            //defaultOutputDir = file('bin/main')
        }
    }
}
