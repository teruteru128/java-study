
plugins {
    id 'java'
    // https://docs.gradle.org/current/userguide/eclipse_plugin.html
    id 'eclipse'
    // https://github.com/java9-modularity/gradle-modules-plugin
    id 'org.javamodularity.moduleplugin' version '1.8.12' apply true
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:32.1.1-jre'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'

    // https://mvnrepository.com/artifact/org.bouncycastle/bcmail-jdk15on
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// thanks for https://github.com/eclipse/eclipse.jdt.ls/issues/957
eclipse {
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.find { it.path == 'src/main/java' }.output = 'build/classes/java/main'

                entries.find { it.path == 'src/main/resources' }.output = 'build/resources/main'

                entries.findAll { it.path.startsWith('/') && !it.path.endsWith('.jar') }.each {
                    it.entryAttributes['module'] = 'true'
                }

                def testSrc = entries.find { it.path == 'src/test/java' }
                testSrc.output = 'build/classes/java/test'
                testSrc.entryAttributes['test'] = 'true'
                testSrc.entryAttributes['optional'] = 'true'
                testSrc.entryAttributes['ignore_optional_problems'] = 'true'
                //testSrc.entryAttributes['module'] = 'true'

                entries.find { it.path == 'src/test/resources' }.output = 'build/resources/test'

                entries.forEach { entry ->
                    def entryIn = { it.find { file(entry.path).equals(it) } }
                    if (entry.kind == 'lib') {
                        entry.entryAttributes['test'] =
                            entryIn(configurations.testRuntimeClasspath) &&
                            !entryIn(configurations.runtimeClasspath)
                    }
                }
            }
        }
    }
}
